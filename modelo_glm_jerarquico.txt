
model {
  # Likelihood
  for (i in 1:N) {
    Y[i] ~ dpois(lambda[i])
    log(lambda[i]) <- beta0 + 
                      beta1 * experiencia[i] + 
                      beta2 * horas[i] + 
                      beta3 * estres[i] + 
                      beta4 * residencia[i] + 
                      beta5 * temporal[i] + 
                      beta6 * formacion[i] + 
                      u[departamento[i]]
  }
  
  # Efectos aleatorios por departamento
  for (j in 1:N_dept) {
    u[j] ~ dnorm(0, tau.u)
  }
  
  # Priors para efectos fijos
  beta0 ~ dnorm(0, 0.01)     # Intercepto
  beta1 ~ dnorm(0, 0.01)     # Experiencia
  beta2 ~ dnorm(0, 0.01)     # Horas trabajadas
  beta3 ~ dnorm(0, 0.01)     # Nivel de estrés
  beta4 ~ dnorm(0, 0.01)     # Contrato residencia
  beta5 ~ dnorm(0, 0.01)     # Contrato temporal
  beta6 ~ dnorm(0, 0.01)     # Formación adicional
  
  # Prior para la precisión de efectos aleatorios
  tau.u ~ dgamma(0.01, 0.01)
  sigma.u <- sqrt(1/tau.u)
}

